apply plugin: 'com.android.application'
apply plugin: 'AndResGuard'

dependencies {
    compile fileTree(include: '*.jar', dir: 'libs')
    compile project(':lib')
    compile project.ANDROID_SUPPORT_DESIGN
    compile project.ANDROID_SUPPORT_V7_CARD_VIEW
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.google.android.gms:play-services-analytics:9.0.1'
}

android {
    compileSdkVersion Integer.parseInt(project.COMPILE_SDK_VERSION)
    buildToolsVersion project.BUILD_TOOLS_VERSION
    dataBinding {
        enabled = true
    }

    Properties p = new Properties()
    p.load(project.rootProject.file('local.properties').newDataInputStream())

    defaultConfig {
        applicationId "com.njust.helper"
        minSdkVersion 14
        targetSdkVersion Integer.parseInt(project.COMPILE_SDK_VERSION)
        versionCode 20160604
        versionName "3.8.2"
        resConfigs "zh"
        buildConfigField "String", "BASE_URL", p.getProperty('localUrl')
    }

    signingConfigs {
        debug {
            keyAlias p.getProperty('keyAlias')
            keyPassword p.getProperty('keyPassword')
            storeFile file('../keystore.key')
            storePassword p.getProperty('storePassword')
        }
        release {
            keyAlias p.getProperty('keyAlias')
            keyPassword p.getProperty('keyPassword')
            storeFile file('../keystore.key')
            storePassword p.getProperty('storePassword')
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    andResGuard {
        mappingFile = null
        use7zip = true
        useSign = true
        keepRoot = false
        whiteList = [
        ]
        compressFilePattern = [
                "*.png",
                "*.jpg",
                "*.jpeg",
                "*.gif",
                "resources.arsc"
        ]
        sevenzip {
            artifact = 'com.tencent.mm:SevenZip:1.1.8'
            //path = "/usr/local/bin/7za"
        }
    }
}

apply plugin: 'com.google.gms.google-services'